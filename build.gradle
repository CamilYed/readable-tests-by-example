plugins {
	id "org.springframework.boot" version "2.5.2"
	id "io.spring.dependency-management" version "1.0.11.RELEASE"
	id "java"
  id "groovy"
  id "application"
}

group = 'tech.allegro.blog'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
	mavenCentral()
}

project.ext.versions = [
    lombok             : '1.18.20',
    vavr               : '1.0.0-alpha-3',
		spock              : '2.0-groovy-3.0'
]

configurations {
  unitTestNotRefactored {
    extendsFrom testCompileClasspath
  }
  integrationTest {
    extendsFrom testRuntimeClasspath
  }
}

dependencies {
  compileOnly "org.projectlombok:lombok:${versions.lombok}"
  annotationProcessor "org.projectlombok:lombok:${versions.lombok}"
	implementation 'org.springframework.boot:spring-boot-starter-web'
  testCompileOnly "org.projectlombok:lombok:${versions.lombok}"
  testAnnotationProcessor "org.projectlombok:lombok:${versions.lombok}"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.spockframework:spock-core:${versions.spock}"
}

sourceSets {
  unitTestNotRefactored {
    groovy {
      compileClasspath += main.output + project.configurations.testCompileClasspath + project.configurations.unitTestNotRefactored
      runtimeClasspath += output + compileClasspath
      srcDir {
        file('src/unitTestNotRefactored/groovy')
      }
    }
    resources.srcDir file('src/unitTestNotRefactored/resources')
  }
  integrationTest {
    groovy {
      compileClasspath += main.output + test.output + project.configurations.testRuntimeClasspath + project.configurations.integrationTest
      runtimeClasspath += output + compileClasspath + project.configurations.testRuntimeClasspath
      srcDir {
        file('src/integrationTest/groovy')
      }
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

test {
  useJUnitPlatform()
  include '**/*Spec.*'
  include '**/*Test.*'
  testLogging {
    exceptionFormat = 'full'
    events = ["STARTED", "PASSED", "SKIPPED", "FAILED"]

  }
}

task unitTestNotRefactored(type: Test) {
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
    events = ["STARTED", "PASSED", "SKIPPED", "FAILED"]
  }
  testClassesDirs = sourceSets.unitTestNotRefactored.output.classesDirs
  classpath = sourceSets.unitTestNotRefactored.runtimeClasspath
  include '**/*Spec.*'
  include '**/*Test.*'
}

task integrationTest(type: Test) {
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
    events = ["STARTED", "PASSED", "SKIPPED", "FAILED"]
  }
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  include '**/*Spec.*'
  include '**/*Test.*'
  systemProperty 'spring.profiles.active', 'integration'
  outputs.upToDateWhen { false }
}

test.dependsOn unitTestNotRefactored, integrationTest

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs +=[ "-parameters", "--enable-preview"]
}

wrapper {
  gradleVersion = '7.1.1'
}
